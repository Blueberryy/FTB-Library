buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = System.properties['mod_version']
if (version == null) {
    version = "Dev"
}

group = "com.feed_the_beast.ftblib"
archivesBaseName = "FTBLib"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.11.2-13.20.0.2285"
    runDir = "run"
    mappings = "snapshot_20161220"
}

repositories {
    ivy {
        name "LatMod"
        artifactPattern "http://mods.latmod.com/[module]/[revision]/[module]-[revision](-[classifier]).[ext]"
    }
    ivy {
        name "Mercurius"
        artifactPattern "https://addons-origin.cursecdn.com/files/2332/590/[module]-[revision](-[classifier]).[ext]"
    }
    maven {
        url "http://maven.k-4u.nl"
    }
}

dependencies {
    compile "Mercurius:Mercurius:1.10.2-1.0.1"
    deobfCompile "com.github.mcjty:compatlayer:1.11.2-0.2.7"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/main/api'
            srcDir 'src/main/lib'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

task apiJar(type: Jar) {
    classifier = 'api'
    from(sourceSets.main.java) {
        include "com/feed_the_beast/ftbl/api/**"
        include "com/feed_the_beast/ftbl/lib/**"
    }
    from(sourceSets.main.output) {
        include "com/feed_the_beast/ftbl/api/**"
        include "com/feed_the_beast/ftbl/lib/**"
    }
    from(sourceSets.main.resources) {
        include "META-INF/ftbl_at.cfg"
    }
    manifest {
        attributes 'FMLAT': 'ftbl_at.cfg'
    }
}

build.dependsOn apiJar

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'ftbl_at.cfg'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLAT': 'ftbl_at.cfg'
    }
}

build.dependsOn deobfJar