buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = System.properties['mod_version']
if (version == null) {
	version = "Dev"
}

group = "com.feed_the_beast.ftblib"
archivesBaseName = "FTBLib"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "1.12-14.21.1.2415"
	runDir = "run"
	mappings = "snapshot_20170720"
}

repositories {
	ivy { name "Mercurius"; artifactPattern "https://addons-origin.cursecdn.com/files/2438/836/[module]-[revision](-[classifier]).[ext]" }
}

dependencies {
	compile "Mercurius:Mercurius:1.12-1.0.6"
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/main/api'
			srcDir 'src/main/lib'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
}

task apiJar(type: Jar) {
	classifier = 'api'
	from(sourceSets.main.java) {
		include "com/feed_the_beast/ftbl/api/**"
		include "com/feed_the_beast/ftbl/lib/**"
	}
	from(sourceSets.main.output) {
		include "com/feed_the_beast/ftbl/api/**"
		include "com/feed_the_beast/ftbl/lib/**"
	}
	from(sourceSets.main.resources) {
		include "META-INF/ftbl_at.cfg"
	}
	manifest {
		attributes 'FMLAT': 'ftbl_at.cfg'
	}
}

build.dependsOn apiJar

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar {
	manifest {
		attributes 'FMLAT': 'ftbl_at.cfg'
	}
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
	manifest {
		attributes 'FMLAT': 'ftbl_at.cfg'
	}
}

build.dependsOn deobfJar